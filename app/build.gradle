apply plugin: 'com.android.application'
apply plugin: 'android-apt'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.squareup.sqldelight'
apply plugin: 'com.noveogroup.android.check'
apply plugin: 'de.mobilej.unmock'

def versionMajor = 0
def versionMinor = 0
def versionPatch = 1
def versionBuild = 0

android {
  compileSdkVersion 24
  buildToolsVersion '24.0.2'
  defaultConfig {
    applicationId 'de.philipphager.disclosure'
    minSdkVersion 21
    targetSdkVersion 24
    versionCode versionMajor * 100000 + versionMinor * 10000 + versionPatch * 100 + versionBuild
    versionName "${versionMajor}.${versionMinor}.${versionPatch}.${versionBuild}"
    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
  }
  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }
  compileOptions {
    targetCompatibility 1.8
    sourceCompatibility 1.8
  }
  lintOptions {
    abortOnError false
  }
}

def supportVersion = '24.2.1'
def autovalueVersion = '1.2'
def butterknifeVersion = '8.4.0'
def daggerVersion = '2.6.1'

dependencies {
  compile fileTree(dir: 'libs', include: ['*.jar'])

  // Database
  compile 'com.squareup.sqlbrite:sqlbrite:0.7.0'

  // Design
  compile "com.android.support:appcompat-v7:${supportVersion}"
  compile "com.android.support:design:${supportVersion}"

  // Dependency injection with dagger 2
  compile "com.google.dagger:dagger:${daggerVersion}"
  apt "com.google.dagger:dagger-compiler:${daggerVersion}"

  // Datetime
  compile 'com.jakewharton.threetenabp:threetenabp:1.0.3'

  // Functional reactive
  compile 'io.reactivex:rxjava:1.1.10'
  compile 'io.reactivex:rxandroid:1.2.1'
  compile 'com.f2prateek.rx.preferences:rx-preferences:1.0.2'

  // Value classes
  provided "com.google.auto.value:auto-value:${autovalueVersion}"
  apt "com.google.auto.value:auto-value:${autovalueVersion}"
  apt 'frankiesardo:auto-parcel:1.0.3'

  // View
  apt "com.jakewharton:butterknife-compiler:${butterknifeVersion}"
  compile "com.jakewharton:butterknife:${butterknifeVersion}"

  // Testing
  testCompile 'junit:junit:4.12'
  testCompile 'org.mockito:mockito-core:1.10.19'
  testCompile 'org.assertj:assertj-core:3.5.2'

  // Stetho, TODO: MOVE TO DEBUG
  compile 'com.facebook.stetho:stetho-okhttp3:1.4.1'
  compile 'com.jakewharton.timber:timber:4.3.1'

  androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
    exclude group: 'com.android.support', module: 'support-annotations'
  })
}

check {
  abortOnError true

  checkstyle {
    config easy()
  }

  findbugs {
    config hard()
  }

  pmd {
    config hard()
  }
}

unMock {
  // URI to download the android-all.jar from. e.g. https://oss.sonatype.org/content/groups/public/org/robolectric/android-all/
  downloadFrom 'https://oss.sonatype.org/content/groups/public/org/robolectric/android-all/4.3_r2-robolectric-0/android-all-4.3_r2-robolectric-0.jar'

  keep 'android.util.SparseArray'
  keep 'com.android.internal.util.ArrayUtils'
}
