apply plugin: 'com.android.application'
apply plugin: 'android-apt'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.squareup.sqldelight'
apply plugin: 'com.noveogroup.android.check'
apply plugin: 'de.mobilej.unmock'
apply plugin: 'findbugs'
apply plugin: 'com.getkeepsafe.dexcount'


def versionMajor = 0
def versionMinor = 0
def versionPatch = 2
def versionBuild = 0

android {
  compileSdkVersion 25
  buildToolsVersion '25.0.0'
  defaultConfig {
    applicationId 'de.philipphager.disclosure'
    minSdkVersion 21
    targetSdkVersion 25
    versionCode versionMajor * 100000 + versionMinor * 10000 + versionPatch * 100 + versionBuild
    versionName "${versionMajor}.${versionMinor}.${versionPatch}.${versionBuild}"
    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
  }
  buildTypes {
    release {
      minifyEnabled true
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }

    debug {
      minifyEnabled true
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
      applicationIdSuffix ".dev"
      versionNameSuffix "-dev"
    }
  }

  compileOptions {
    targetCompatibility 1.8
    sourceCompatibility 1.8
  }

  lintOptions {
    abortOnError false
  }
}

def supportVersion = '25.1.1'
def autovalueVersion = '1.2'
def autovalueGsonVersion = '0.4.4'
def autovalueParcelVersion = '0.2.5'
def butterknifeVersion = '8.4.0'
def daggerVersion = '2.8'
def retrofitVersion = '2.1.0'

dependencies {
  compile fileTree(dir: 'libs', include: ['*.jar'])

  // Database
  compile 'com.squareup.sqlbrite:sqlbrite:0.8.0'
  compile 'com.readystatesoftware.sqliteasset:sqliteassethelper:2.0.1'

  // Design
  compile "com.android.support:appcompat-v7:${supportVersion}"
  compile "com.android.support:support-annotations:${supportVersion}"
  compile "com.android.support:cardview-v7:${supportVersion}"
  compile "com.android.support:preference-v7:${supportVersion}"
  compile "com.android.support:transition:${supportVersion}"
  compile 'com.takisoft.fix:preference-v7:25.1.1.0'

  // Dependency injection with dagger 2
  compile "com.google.dagger:dagger:${daggerVersion}"
  apt "com.google.dagger:dagger-compiler:${daggerVersion}"

  // Datetime
  compile 'com.jakewharton.threetenabp:threetenabp:1.0.3'
  testCompile 'org.threeten:threetenbp:1.3.2'

  // Functional reactive
  compile 'io.reactivex:rxjava:1.1.10'
  compile 'io.reactivex:rxandroid:1.2.1'
  compile 'com.f2prateek.rx.preferences:rx-preferences:1.0.2'

  // HTTP and REST client
  compile "com.squareup.retrofit2:retrofit:${retrofitVersion}"
  compile "com.squareup.retrofit2:converter-gson:${retrofitVersion}"
  compile "com.squareup.retrofit2:adapter-rxjava:${retrofitVersion}";
  compile 'com.squareup.okhttp3:okhttp:3.4.2'
  compile 'com.google.code.gson:gson:2.8.0'

  // Value classes and serialization helpers
  compile "com.google.auto.value:auto-value:${autovalueVersion}"
  apt "com.google.auto.value:auto-value:${autovalueVersion}"
  apt "com.ryanharter.auto.value:auto-value-gson:${autovalueGsonVersion}"
  provided "com.ryanharter.auto.value:auto-value-gson:${autovalueGsonVersion}"
  apt "com.ryanharter.auto.value:auto-value-parcel:${autovalueParcelVersion}"
  apt 'com.gabrielittner.auto.value:auto-value-with:1.0.0'

  // View
  apt "com.jakewharton:butterknife-compiler:${butterknifeVersion}"
  compile "com.jakewharton:butterknife:${butterknifeVersion}"
  compile 'com.aurelhubert:ahbottomnavigation:1.5.1'
  compile 'com.kofigyan.stateprogressbar:stateprogressbar:0.0.3'
  compile 'me.gujun.android.taggroup:library:1.4@aar'

  // APK parser
  compile 'net.dongliu:apk-parser:2.1.6'
  compile 'org.smali:baksmali:2.1.3'
  compile 'com.jakewharton.android.repackaged:libcore-dex:7.1.0_r7'

  // Memory leak detection
  debugCompile 'com.squareup.leakcanary:leakcanary-android:1.5'
  releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5'
  testCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5'

  // Testing
  testCompile 'junit:junit:4.12'
  testCompile 'org.mockito:mockito-core:1.10.19'
  testCompile 'org.assertj:assertj-core:3.5.2'

  testCompile('org.powermock:powermock-api-mockito:1.6.5') {
    exclude module: 'hamcrest-core'
    exclude module: 'objenesis'
  }

  testCompile('org.powermock:powermock-module-junit4:1.6.5') {
    exclude module: 'hamcrest-core'
    exclude module: 'objenesis'
  }

  // Stetho
  compile 'com.facebook.stetho:stetho-okhttp3:1.4.1'
  compile 'com.jakewharton.timber:timber:4.3.1'

  androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
    exclude group: 'com.android.support', module: 'support-annotations'
  })
}

check {
  abortOnError true

  checkstyle {
    config '../config/checkstyle.xml'
  }

  pmd {
    config hard()
  }
}

// Fix for findbugs error according to this thread:
// https://github.com/noveogroup/android-check/issues/14
task androidFindbugs(type: FindBugs, overwrite: true) {
  description 'Run findbugs'
  group 'verification'

  classes = files("$project.buildDir/intermediates/classes/debug")
  source = fileTree('src/main/java')
  classpath = files()

  ignoreFailures = true
  effort = "default"
  reportLevel = "medium"

  excludeFilter = file("../config/findbugs.xml")

  reports {
    xml.enabled = false
    html.enabled = !xml.enabled
    html.destination new File(project.buildDir, 'reports/findbugs.html')
  }
}
androidFindbugs.dependsOn 'assembleDebug'

unMock {
  // Keep some Android classes for testing, without using Robolectric.
  downloadFrom 'https://oss.sonatype.org/content/groups/public/org/robolectric/android-all/4.3_r2-robolectric-0/android-all-4.3_r2-robolectric-0.jar'

  keep 'android.util.SparseArray'
  keep 'com.android.internal.util.ArrayUtils'
  keep 'android.content.ContentValues'
  keep 'android.content.ComponentName'
  keep 'android.content.Intent'
  keep 'android.content.pm.PackageInfo'
  keep 'android.net.Uri'
  keep 'android.os.Bundle'
}
